<S> ::= empty | <RULESRUN>
<RULESRUN> ::= <RULERUN> | <RULERUN> <RULESRUN>
<RULERUN> ::= if ( <ANTECEDENTS> ) { <CONSEQUENTS> }
<ANTECEDENTS> ::= <BOOLANTECEDENT> | ( <BOOLANTECEDENT> <OPRBOOLANTECEDENT> <ANTECEDENTS> )
<BOOLANTECEDENT> ::= <NUMCLAUSE> == <NUMCLAUSE> | <NUMCLAUSE> != <NUMCLAUSE> | <NUMCLAUSE> > <NUMCLAUSE> | <NUMCLAUSE> < <NUMCLAUSE> | <NUMCLAUSE> <= <NUMCLAUSE> | <NUMCLAUSE> >= <NUMCLAUSE>
<NUMCLAUSE> ::= <NUMVALUE> | ( <NUMVALUE> <OPRNUMVALUE> <NUMCLAUSE> )
<NUMVALUE> ::= REALVALUE | INTVALUE | <MATH> | <ROBOTDATA>
<MATH> ::= Math.cos( <NUMCLAUSE> ) | Math.sin( <NUMCLAUSE> ) | Math.tan( <NUMCLAUSE> ) | Math.sqrt( <NUMCLAUSE> ) | Math.pow( <NUMCLAUSE> , <NUMCLAUSE> )
<ROBOTDATA> ::= getGunHeadingRadians() | getGunTurnRemainingRadians() | getHeadingRadians() | getRadarHeadingRadians() | getRadarTurnRemainingRadians() | getTurnRemainingRadians() | getEnergy() | getGunCoolingRate() | getGunHeat() | getVelocity() | getX() | getY()
<OPRNUMVALUE> ::= + | - | * | %
<OPRBOOLANTECEDENT> ::= && | ||
<CONSEQUENTS> ::= <CONSEQUENT> | <CONSEQUENT> <CONSEQUENTS>
<CONSEQUENT> ::= doNothing(); | setAhead( NUMVALUE ); | setBack( NUMVALUE ); | setFire( NUMVALUE ); | setTurnGunLeftRadians( NUMVALUE ); | setTurnGunRightRadians( NUMVALUE ); | setTurnLeftRadians( NUMVALUE ); | setTurnRadarLeftRadians( NUMVALUE ); | setTurnRadarRightRadians( NUMVALUE ); | setTurnRightRadians( NUMVALUE );